  p68h11
	public	cargon,cargoop
	extern	secuizq
$const.def	
****************************************************************************************************************
* CARGON
*	
* Parametros recibidos: Origen de la cadena que contiene a operando 1, operador y operando 2 por IX, Ubicación del 
*caracter a cargar por IY
* Parametros entregados: Nada
****************************************************************************************************************
	rseg	CODE

cargon	equ *
	psha
	pshb
	pshx
	pshy
    
	ldaa	10,x	; Cargo el operador actual
	bne	cargop2	; Si ya hay operando: cargo el segundo numero
	ldaa	1,x
	bne	fcargon	; si el operando 1 esta lleno doy por terminado la carga
	ldab	0,y	
	jsr	secuizq	; Cargo el nuevo digito en la memoria donde guardo los operandos en bcd
	tsx		; Creo un frame pointer
	ldx	10,x	; Tomo la dirección de la memoria de los display
	jsr	secuizq	; Cargo el nuevo digito al arreglo que muestra el display
	jmp	fcargon
	
cargop2	ldaa	5,x
	bne	fcargon	; si el operando 1 esta lleno doy por terminado la carga
	ldab	#5
	abx		; Apunto al comienzo del numero en bcd
	ldab	0,y		
	jsr	secuizq	; Cargo el nuevo digito en la memoria donde guardo los operandos en bcd
	tsx		; Creo un frame pointer
	ldx	10,x	; Tomo la dirección de la memoria de los display
	jsr	secuizq	; Cargo el nuevo digito al arreglo que muestra el display
	jmp	fcargon
   
fcargon    puly
    pulx
    pulb
    pula
	rts

****************************************************************************************************************

****************************************************************************************************************
* CARGOOP
*	
* Parametros recibidos: Dir de memoria donde cargar el operando por IX, memorias del display por IY.
* Parametros entregados: Nada
****************************************************************************************************************
	rseg	CODE
cargoop	equ *
	
	pshb
	pshx
	pshy

	ldab	0,x
	bne	fcargop	; Si ya hay operando: termino la subrutina
	staa	0,x
	pshy
	pulx
	jsr	secuizq
    
fcargop puly
	pulx
	pulb
	rts

****************************************************************************************************************
	END